plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'com.diffplug.spotless' version '6.11.0'
}

allprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly("org.projectlombok:lombok")
        testAnnotationProcessor("org.projectlombok:lombok")
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    spotless {
        java {
            importOrder()
            removeUnusedImports()
            googleJavaFormat().aosp()

            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    tasks.named('compileJava') {
        dependsOn(rootProject.tasks.named('updateGitHooks'))
    }
}

tasks.register('updateGitHooks', Copy) {
    from 'scripts/pre-commit'
    into '.git/hooks'
}